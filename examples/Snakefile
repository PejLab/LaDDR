from pathlib import Path

def dataset_samples(dataset):
    with open(input_dir / f'samples.{dataset}.txt') as f:
        return [l.strip() for l in f]

ref_genome = 'path/to/ref/genome'
input_dir = Path('data_input')
ref_anno = input_dir / 'Homo_sapiens.GRCh38.106.chr1_0-2Mb.gtf'
bam_dir = input_dir / 'bam'
working_dir = Path('data_snakemake')

modalities = ['alt_TSS', 'alt_polyA', 'expression', 'isoforms', 'splicing', 'stability']
N_BATCHES = 3 # Based on number of genes and batch size

with open(input_dir / 'datasets.txt', 'r') as f:
    datasets = [l.strip() for l in f]

localrules:
    get_chrom_lengths,
    # latent_pheno_groups,

rule all:
    input:
        # expand(working_dir / 'gene_bins' / 'batch_{batch}.bed.gz', batch=range(N_BATCHES)),
        # expand(working_dir / 'covg_binned_dset1' / 'batch_{batch}.npz', batch=range(N_BATCHES)),
        # working_dir / 'covg_norm_dset1' / 'batch_0.npy',
        # working_dir / 'models' / 'models_batch_0.pickle',
        expand(working_dir / 'latent_phenos.{dset}.tsv.gz', dset=datasets),

rule get_chrom_lengths:
    """Get chromosome lengths from genome FASTA index for bedtools"""
    input:
        f'{ref_genome}.fai',
    output:
        input_dir / 'chr_lengths.genome',
    shell:
        'cut -f1,2 {input} > {output}'

rule collapse_annotation:
    """Merge the exons for all isoforms of a gene into one set of non-overlapping exon regions."""
    input:
        ref_anno,
    output:
        working_dir / 'collapsed.gtf',
    shell:
        'python ../scripts/collapse_annotation.py {input} {output} --collapse_only'

rule get_gene_bins:
    """Divide gene features into bins"""
    input:
        gtf = working_dir / 'collapsed.gtf',
        chrom = input_dir / 'chr_lengths.genome',
    output:
        beds = expand(str(working_dir / 'gene_bins' / 'batch_{batch}.bed.gz'), batch=range(N_BATCHES)),
    params:
        # n_bins = 16,
        bin_width_coding = 16,
        bin_width_noncoding = 128,
        batch_size = 20,
        outdir = working_dir / 'gene_bins',
    shell:
        """
        python ../scripts/get_gene_bins.py \
            --gtf {input.gtf} \
            --chromosomes {input.chrom} \
            --binning-method bin-width \
            --bin-width-coding {params.bin_width_coding} \
            --bin-width-noncoding {params.bin_width_noncoding} \
            --batch-size {params.batch_size} \
            --outdir {params.outdir}
        """

rule get_bigwig:
    """Convert BAM to bigWig for bp-level coverage"""
    input:
        bam = bam_dir / '{sample_id}.bam',
    output:
        bw = working_dir / 'covg_bigwig' / '{sample_id}.bw',
    params:
        bigwig_dir = working_dir / 'covg_bigwig',
    threads: 16,
    shell:
        """
        mkdir -p {params.bigwig_dir}
        bamCoverage -b {input.bam} -o {output.bw} -of bigwig --binSize 1 -p {threads}
        """

rule bin_coverage:
    """Average RNA-Seq read coverage per feature bin, combine samples"""
    input:
        bw = lambda w: expand(str(working_dir / 'covg_bigwig' / '{sample_id}.bw'), sample_id=dataset_samples(w.dset)),
        bed = working_dir / 'gene_bins'/ 'batch_{batch}.bed.gz',
        chrom = input_dir / 'chr_lengths.genome',
    output:
        working_dir / 'covg_binned_{dset}' / 'batch_{batch}.npz',
    params:
        covg_binned_dir = str(input_dir / 'covg_binned_{dset}'),
        labels = lambda w: ' '.join(dataset_samples(w.dset)),
    shell:
        """
        mkdir -p {params.covg_binned_dir}
        multiBigwigSummary BED-file \
            --bwfiles {input.bw} \
            --labels {params.labels} \
            --BED {input.bed} \
            -o {output}
        """

rule prepare_coverage:
    """Assemble per-sample coverage count files, normalize, regress out explicit phenotypes, and bin into batches for PCA"""
    input:
        covg = working_dir / 'covg_binned_{dset}' / 'batch_{batch}.npz',
        bins = working_dir / 'gene_bins' / 'batch_{batch}.bed.gz',
        phenos = expand('data_input/phenotypes/{modality}.bed.gz', modality=modalities),
        phenos_list = 'data_input/pheno_files.txt',
    output:
        covg = working_dir / 'covg_norm_{dset}' / 'batch_{batch}.npy',
        bins = working_dir / 'covg_norm_{dset}' / 'batch_{batch}.bins.tsv.gz',
    params:
        covg_binned_dir = str(working_dir / 'covg_binned_{dset}'),
        bins_dir = working_dir / 'gene_bins',
        covg_norm_dir = str(working_dir / 'covg_norm_{dset}'),
    resources:
        mem_mb = 32000,
    shell:
        """
        mkdir -p {params.covg_norm_dir}
        python ../latent_RNA.py prepare \
            --input-covg-dir {params.covg_binned_dir} \
            --bins-dir {params.bins_dir} \
            --batch {wildcards.batch} \
            --pheno-paths-file {input.phenos_list} \
            --output-dir {params.covg_norm_dir}
        """

rule fit_models:
    """Fit models to latent features"""
    input:
        covg_norm = expand(str(working_dir / 'covg_norm_{dset}' / 'batch_{{batch}}.npy'), dset=datasets),
        bins = expand(str(working_dir / 'covg_norm_{dset}' / 'batch_{{batch}}.bins.tsv.gz'), dset=datasets),
    output:
        models = working_dir / 'models' / 'models_batch_{batch}.pickle',
    params:
        covg_norm_dirs = expand(str(working_dir / 'covg_norm_{dset}'), dset=datasets),
        covg_norm_dirs_file = working_dir / 'covg_norm_dirs.txt',
        models_dir = working_dir / 'models',
        var_expl_max = 0.80,
        n_pcs_max = 16,
    shell:
        """
        mkdir -p {params.models_dir}
        printf '%s\\n' {params.covg_norm_dirs} > {params.covg_norm_dirs_file}
        python ../latent_RNA.py fit \
            --norm-covg-dir-file {params.covg_norm_dirs_file} \
            --batch {wildcards.batch} \
            --models-dir {params.models_dir} \
            -v {params.var_expl_max} \
            -n {params.n_pcs_max}
        """

rule get_latent_phenotypes:
    """Apply models to generate latent phenotypes"""
    input:
        covg_norm = expand(str(working_dir / 'covg_norm_{{dset}}' / 'batch_{batch}.npy'), batch=range(N_BATCHES)),
        models = expand(str(working_dir / 'models' / 'models_batch_{batch}.pickle'), batch=range(N_BATCHES)),
    output:
        phenos = working_dir / 'latent_phenos.{dset}.tsv.gz',
    params:
        covg_norm_dir = str(working_dir / 'covg_norm_{dset}'),
        models_dir = working_dir / 'models',
        n_batches = N_BATCHES,
    shell:
        """
        python ../latent_RNA.py transform \
            --norm-covg-dir {params.covg_norm_dir} \
            --models-dir {params.models_dir} \
            --n-batches {params.n_batches} \
            --output {output}
        """
