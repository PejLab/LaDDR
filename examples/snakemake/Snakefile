from pathlib import Path
import numpy as np
import pandas as pd

configfile: 'config.yaml'
working_dir = Path('.')

BATCH_FILE = working_dir / 'info' / 'n_batches.txt'
assert BATCH_FILE.exists(), f'{BATCH_FILE} not found. Run latent-rna setup first.'
with open(BATCH_FILE, 'r') as f:
    N_BATCHES = int(f.read())

assert config['input']['coverage']['method'] == 'manifest'
sample_table = pd.read_csv(config['input']['coverage']['manifest'], sep='\t', names=['dataset', 'sample', 'path'])
if 'directory' in config['input']['coverage']:
    # Prepend coverage directory to paths if specified
    covg_dir = Path(config['input']['coverage']['directory'])
    sample_table['path'] = sample_table['path'].apply(lambda p: str(covg_dir / p))

datasets = sample_table['dataset'].unique().tolist()

rule all:
    input:
        # expand(working_dir / 'gene_bins' / 'batch_{batch}.bed.gz', batch=range(N_BATCHES)),
        # working_dir / 'covg_norm' / 'dset1' / 'batch_0.npy',
        # working_dir / 'models' / 'models_batch_0.pickle',
        expand(working_dir / 'phenotypes' /'latent_phenos.{dataset}.tsv.gz', dataset=datasets),
        # expand(working_dir / 'inspect' / 'inspect.{gene_id}.{dataset}.tsv.gz', gene_id=['ENSG00000008128'], dataset=datasets),

rule latent_rna_binning:
    """Divide gene features into bins"""
    input:
        genes = working_dir / 'info' / 'genes.tsv',
        exons = working_dir / 'info' / 'exons.tsv.gz',
        var_per_bin = working_dir / 'info' / 'var_per_bin.txt',
        bw = sample_table['path'].tolist(),
    output:
        beds = working_dir / 'gene_bins' / 'batch_{batch}.bed.gz',
    resources:
        mem_mb = 32000,
        runtime = '12h',
    shell:
        """
        latent-rna binning -b {wildcards.batch}
        """

rule latent_rna_coverage:
    """Assemble coverage across samples, normalize, regress out explicit phenotypes, and bin into batches for PCA"""
    input:
        bw = lambda w: sample_table.loc[sample_table['dataset'] == w.dataset, 'path'].tolist(),
        bins = working_dir / 'gene_bins' / 'batch_{batch}.bed.gz',
        median_covg = working_dir / 'info' / 'median_coverage.txt',
        # phenos = config['input']['pheno_paths'],
    output:
        covg = working_dir / 'covg_norm' / '{dataset}' / 'batch_{batch}.npy',
        covg_norm_bins = working_dir / 'covg_norm' / '{dataset}' / 'batch_{batch}.bins.tsv.gz',
    resources:
        mem_mb = 32000,
        runtime = '12h',
    shell:
        """
        latent-rna coverage -d {wildcards.dataset} -b {wildcards.batch}
        """

rule latent_rna_fit:
    """Fit models to latent features"""
    input:
        covg_norm = expand(str(working_dir / 'covg_norm' / '{dataset}' / 'batch_{{batch}}.npy'), dataset=datasets),
        covg_norm_bins = expand(str(working_dir / 'covg_norm' / '{dataset}' / 'batch_{{batch}}.bins.tsv.gz'), dataset=datasets),
    output:
        models = working_dir / 'models' / 'models_batch_{batch}.pickle',
    shell:
        """
        latent-rna fit -b {wildcards.batch}
        """

rule latent_rna_transform:
    """Apply models to generate latent phenotypes"""
    input:
        covg_norm = expand(str(working_dir / 'covg_norm' / '{{dataset}}' / 'batch_{batch}.npy'), batch=range(N_BATCHES)),
        covg_norm_bins = expand(str(working_dir / 'covg_norm' / '{{dataset}}' / 'batch_{batch}.bins.tsv.gz'), batch=range(N_BATCHES)),
        models = expand(str(working_dir / 'models' / 'models_batch_{batch}.pickle'), batch=range(N_BATCHES)),
    output:
        phenos = working_dir / 'phenotypes' / 'latent_phenos.{dataset}.tsv.gz',
    shell:
        """
        latent-rna transform -d {wildcards.dataset}
        """

rule latent_rna_inspect:
    """Extract model data for visualization"""
    input:
        genes = working_dir / 'info' / 'genes.tsv',
        covg_norm = expand(str(working_dir / 'covg_norm' / '{{dataset}}' / 'batch_{batch}.npy'), batch=range(N_BATCHES)),
        covg_norm_bins = expand(str(working_dir / 'covg_norm' / '{{dataset}}' / 'batch_{batch}.bins.tsv.gz'), batch=range(N_BATCHES)),
        models = expand(str(working_dir / 'models' / 'models_batch_{batch}.pickle'), batch=range(N_BATCHES)),
        phenos = working_dir / 'phenotypes' / 'latent_phenos.{dataset}.tsv.gz',
    output:
        inspect = working_dir / 'inspect' / 'inspect.{gene_id}.{dataset}.tsv.gz',
    shell:
        """
        latent-rna inspect -g {wildcards.gene_id} -d {wildcards.dataset}
        """
